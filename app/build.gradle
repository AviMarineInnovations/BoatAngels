buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

def final yourApplicationId = 'in.avimarine.boatangels'
android.variantFilter { variant ->
    if (variant.buildType.name == 'release' && variant.getFlavors().get(0).name == 'dev') {
        variant.setIgnore(true)
    }
}
android {
    signingConfigs {
        if (System.getenv("AMIT")) {
            release {
                // Load keystore
                def keystorePropertiesFile = rootProject.file("keystore.properties")
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                storeFile = file(keystoreProperties['storeFile'])
                storePassword = keystoreProperties['storePassword']
                keyAlias = keystoreProperties['keyAlias']
                keyPassword = keystoreProperties['keyPassword']
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdkVersion 30
    defaultConfig {
        applicationId = yourApplicationId
        //These are the values for the authorities and account types.
        //Reference them in the java files with e.g. BuildConfig.ACCOUNT_TYPE.
        buildConfigField "String", "ACCOUNT_TYPE", "\"${applicationId}.account\""
        buildConfigField "String", "AUTHORITY", "\"${applicationId}.provider\""
        //Reference them in .xml files.
        multiDexEnabled true
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 4
        versionName "1.03"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
//        resConfigs "en", "iw"
        vectorDrawables.useSupportLibrary = true
        ext.enableCrashlytics = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (System.getenv("AMIT")) {
                signingConfig signingConfigs.release
            }
        }
    }
    flavorDimensions "standard"
    productFlavors {
        dev {
            dimension "standard"
            applicationIdSuffix ".dev"
            //Reference them again so they get overwritten by the flavor.
            buildConfigField "String", "ACCOUNT_TYPE", "\"${applicationId}.account\""
            buildConfigField "String", "AUTHORITY", "\"${applicationId}.provider\""
        }
        stage {
            dimension "standard"
            applicationIdSuffix ".stage"
            //Reference them again so they get overwritten by the flavor.
            buildConfigField "String", "ACCOUNT_TYPE", "\"${applicationId}.account\""
            buildConfigField "String", "AUTHORITY", "\"${applicationId}.provider\""
        }
        production {
            dimension "standard"
            //Reference them again so they get overwritten by the flavor.
            buildConfigField "String", "ACCOUNT_TYPE", "\"${applicationId}.account\""
            buildConfigField "String", "AUTHORITY", "\"${applicationId}.provider\""
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.0'
    implementation platform('com.google.firebase:firebase-bom:28.3.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-core:16.0.5'
    implementation 'com.google.firebase:firebase-auth:16.0.5'
    implementation 'com.google.firebase:firebase-crash:16.2.1'
    implementation 'com.google.firebase:firebase-storage:16.0.4'
    implementation 'com.google.firebase:firebase-firestore:17.1.2'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.0'
    implementation 'com.google.firebase:firebase-analytics:19.0.0'
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation 'com.firebaseui:firebase-ui-auth:4.2.1'
    implementation 'com.firebaseui:firebase-ui-firestore:4.2.1'
    implementation 'com.firebaseui:firebase-ui-storage:4.2.1'
    implementation 'io.doorbell:android-sdk:0.2.8@aar'
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
    implementation 'com.jakewharton:butterknife:10.2.3'
    implementation 'com.hbb20:ccp:2.1.1'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.github.pwittchen:weathericonview:1.1.0'
    implementation 'devlight.io:navigationtabbar:1.2.5'
    implementation 'org.jetbrains:annotations:15.0'
    implementation('com.crashlytics.sdk.android:answers:1.4.4@aar') {
        transitive = true
    }
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'

}
